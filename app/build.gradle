/*
 * Copyright 2018 Farbod Salamat-Zadeh
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.farbodsz.familytree"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    // Testing frameworks
    testImplementation 'junit:junit:4.12'

    // Include libraries in the libs directory
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin standard library
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android support libraries
    implementation "com.android.support:appcompat-v7:$android_support_lib_version"
    implementation "com.android.support:design:$android_support_lib_version"
    implementation "com.android.support:cardview-v7:$android_support_lib_version"
    implementation "com.android.support:recyclerview-v7:$android_support_lib_version"

    // DateTime library - Java 8 APIs require minSdk >= 26
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    // Other libraries
    implementation 'de.hdodenhof:circleimageview:2.2.0'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs"

    // Do not create index pages for empty packages
    skipEmptyPackages = true

    // Include private members
    includeNonPublic = true

    // Specifies the location of the project source code on the web (source links)
    linkMapping {
        // Source directory
        dir = "src/main/kotlin"

        // URL showing where the source code can be accessed through the web browser
        url = "https://github.com/FarbodSalamat-Zadeh/FamilyTreeApp/tree/master/app/src/main/kotlin"

        // Suffix which is used to append the line number to the URL (#L for GitHub)
        suffix = "#L"
    }

    externalDocumentationLink {
        // Root URL of the generated documentation to link with
        url = new URL("https://developer.android.com/reference/")
    }

    // Don't generate docs for other libraries
    packageOptions {
        prefix = "kotlin"
        suppress = true
    }
    packageOptions {
        prefix = "android"
        suppress = true
    }
    packageOptions {
        prefix = "com.jakewharton"
        suppress = true
    }
    packageOptions {
        prefix = "de.hdodenhof"
        suppress = true
    }
}
